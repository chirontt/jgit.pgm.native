plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.9.5'
}

wrapper {
    gradleVersion = '7.2'
    distributionType = Wrapper.DistributionType.ALL
}

group = 'com.github.chirontt'
version = '5.13.0'
description = 'JGit PGM native executable built by GraalVM'

ext {
    mainClassName = 'com.github.chirontt.jgit.graalvm.NativeMain'
    jgitReleaseVersion = '5.13.0.202109080827-r'
    graalvmVersion = '21.2.0'
    xzVersion = '1.9'
    currentPlatform = getCurrentPlatform()
    nativeImageDirName = "native-image-$currentPlatform"
}

//detect the OS (assuming 64-bit, on Intel/AMD hardware)
private static String getCurrentPlatform() {
    def currentOS = org.gradle.internal.os.OperatingSystem.current()
    return currentOS.isLinux() ? 'linux' :
           currentOS.isMacOsX() ? 'macos' :
           currentOS.isWindows() ? 'windows' :
           'unknown'
}

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava {
    options.release = 11  //use GraalVM 21+ for compiling
    options.encoding = 'UTF-8'
}

dependencies {
    implementation("org.eclipse.jgit:org.eclipse.jgit.pgm:$jgitReleaseVersion") {
        //exclude the transitive Apache HttpClient 4.5.13
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    //include the local httpclient 4.5.13 version with fix for native image usage
    implementation files('lib/httpclient-4.5.13-native-image-fix.jar')
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'commons-codec:commons-codec:1.11'

    //optional, but required by GraalVM native-image build
    implementation "org.tukaani:xz:$xzVersion"

    //for compiling GraalVM substitution classes
    compileOnly "org.graalvm.nativeimage:svm:$graalvmVersion"
}

sourceSets {
    main {
        resources {
            //include current platform's GraalVM native-image configuration files
            srcDirs = ["src/graal-cfg/$currentPlatform"]
        }
    }
}

application {
    mainClass = project.mainClassName
    applicationName = project.name
}

run {
    //get system properties specified from the command line (for debugging, etc.)
    //and pass them on to the running application's JVM
    systemProperties = System.getProperties()

    //use the following jvmArgs for as many different run scenarios as possible,
    //and for all the code-execution paths as much as possible,
    //to generate (or merge with) the GraalVM native-image configuration files
    //in the src/graal-cfg/$currentPlatform/META-INF/native-image directory.
    //This directory is read by GraalVM during the native-image build.

    //jvmArgs = ["-agentlib:native-image-agent=config-merge-dir=src/graal-cfg/$currentPlatform/META-INF/native-image"]
}

//copy the font config files from the JDK
task copyFontConfigFiles(type: Copy) {
    def jdkHome = System.getProperty('java.home')
    from ("$jdkHome/lib") {
        include 'fontconfig.bfc', 'fontconfig.properties.src'
    }
    into "$buildDir/$nativeImageDirName/lib"
}

//remove unneeded .dll/.txt files
task removeUnneededDllFiles {
    doLast {
        ant.move(todir: "$buildDir/tmp/$nativeImageDirName") {
            fileset(dir: "$buildDir/$nativeImageDirName") {
                include name: '*.dll'
                include name: '*.txt'
                //keep the shim DLLs
                exclude name: 'java.dll'
                exclude name: 'jvm.dll'
            }
        }
    }
}

//create a stand-alone executable uber jar
//including all dependencies
task uberJar(type: Jar) {
    archiveClassifier = 'no-deps'

    with jar
    manifest {
        attributes(
            'Main-Class': project.mainClassName,
            'Created-By': System.getProperty('java.runtime.version') + ' (' + System.getProperty('java.vendor') + ')',
            'Built-By': System.getProperty('user.name'),
            'Gradle-Version': 'Gradle ' + gradle.getGradleVersion(),
            'Multi-Release': 'true',
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    duplicatesStrategy 'exclude'
}

graalvmNative {
    binaries {
        main {
            imageName = project.name
            mainClass = project.mainClassName
            verbose = true
            fallback = false

            //packages/classes to be initialized at native image build time
            def buildTimeInitClasses = [
                'com.google.gson',
                'com.sun.beans.introspect.ClassInfo',
                'com.sun.beans.introspect.PropertyInfo',
                'java.beans.Introspector',
                'org.apache.log4j',
                'org.apache.sshd',
                'org.eclipse.jetty',
                'org.eclipse.jgit',
                'org.slf4j',
            ]

            //packages/classes to be initialized at native image run time
            def runTimeInitClasses = [
                'org.eclipse.jgit.internal.storage.file.WindowCache',
                'org.eclipse.jgit.lib.internal.WorkQueue',
                'org.eclipse.jgit.lib.RepositoryCache',
                'org.eclipse.jgit.transport.HttpAuthMethod',
                'org.eclipse.jgit.util.FileUtils',
            ]

            if (currentPlatform == 'linux') {
                jvmArgs.add('-Djava.awt.headless=false')
            }

            buildArgs.add('--gc=epsilon')  //no garbage collector
            buildArgs.add('--enable-url-protocols=http,https')
            buildArgs.add('--initialize-at-build-time=' + buildTimeInitClasses.join(','))
            buildArgs.add('--initialize-at-run-time=' + runTimeInitClasses.join(','))
            buildArgs.add('--native-image-info')
            buildArgs.add("-H:Path=$buildDir/$nativeImageDirName")
            buildArgs.add("-H:TempDirectory=$buildDir/tmp/$nativeImageDirName")
            //buildArgs.add('-H:+TraceNativeToolUsage')

            useFatJar = false
        }
    }
}

tasks.named("nativeCompile") {
    //use the uber jar as long classpath becomes a problem in Windows:
    dependsOn uberJar
    classpathJar = uberJar.archiveFile
    if (currentPlatform == 'windows') {
        finalizedBy copyFontConfigFiles, removeUnneededDllFiles
    }
}

